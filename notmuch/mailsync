#!/bin/zsh

MAILDIR=$HOME/mail
# Exit if any command fails
set -e

CONFIGDIR=$HOME/.config/notmuch
GITDIR=$MAILDIR/.notmuch/git
HOST=$(hostname)

# Already running?
zmodload zsh/net/socket
SOCKET=/tmp/mailsync.$UID
zsocket -l "$SOCKET" 2>/dev/null
if [ $? != 0 ]; then
    echo "Process already running."
    exit 1
fi

zshexit() {
    rm "$SOCKET"
}
TRAPINT() {
    return $(( 128 + $1 ))
}

# Check the DISPLAY environment variable (needed for D-Bus and awesome-client)
[[ -z "$DISPLAY" ]] && export DISPLAY=:0.0

# Tell awesome the update begins
n=$(notmuch count tag:unread)
echo "tb_mails_updating(true)\ntb_mails_set_count($n)" | awesome-client

# Removes messages with the "deleted" tag
(( $(notmuch count tag:deleted) > 0 )) && notmuch search --output=files tag:deleted | xargs -rd'\n' rm

# Several functions are used to control the sync flow. backup(), gitmuch_load()
# and fetch_mail() are run asynchronously, so we need to take special care of
# their outputs in order to leave them readable.

# Backup the DB
function backup() {
    # Is a recent backup available?
    BACKUPFILE=$CONFIGDIR/db-$(date +"%Y%m%d").xz

    if [ ! -e "$BACKUPFILE" ]; then
        echo -n "\r\t\t\t\tBackup..."
        notmuch dump | xz > "$BACKUPFILE" || (echo "\nBackup failed" && exit 1)

        # Remove backups older than a month
        find "$CONFIGDIR" -name 'db-*.xz' -and -mtime +30 -exec rm \{\} +

        echo -n "\r\t\t\t\tBackup OK"
    fi
}

# Save the DB for sync with gitmuch
function gitmuch_save() {
    echo -n "\r\t\tGitmuch save..."
    pushd -q "$GITDIR"
    notmuch dump tags
    if git commit -a --dry-run >/dev/null; then
        git commit -a -m "Dump on $HOST" >/dev/null
    fi
    popd -q
    echo -n "\r\t\tGitmuch save OK"
}

# Load (and merge) a remote DB
function gitmuch_load() {
    echo "Gitmuch load..."
    pushd -q "$GITDIR"

    local localrev newrev tmpfile
    localrev=$(cat .git/refs/heads/master)

    # Try to do a fast-forward pull. If it does not, merge it with manual intervention.
    git pull -q --ff-only || do_merge
    newrev=$(cat .git/refs/heads/master)

    if [[ "$localrev" != "$newrev" ]]; then
        # There were changes: apply them to the DB
        tmpfile=$(mktemp)
        git diff '$localrev..$newrev' | grep '^+[^++ b/]' | cut -c 2- > "$tmpfile"
        notmuch restore "$tmpfile"
        rm "$tmpfile"
    fi
    
    # Push changes (local + merge)
    git push -q

    popd -q
}

# Fetch new mails
function fetch_mail() {
    echo -n "\rFetch mail..."
    offlineimap -o -u Noninteractive.Quiet || (echo "\nFetch mail failed" && exit 1)
    echo -n "\rFetch mail OK"
}

# Update the notmuch DB
function update_db() {
    echo "Updating the notmuch database..."
    nosync notmuch new || exit 1
}

# Autotag new messages
function autotag() {
    echo "Autotagging new messages..."
    nosync $CONFIGDIR/autotag.py || exit 1
}

# Run first steps asynchronously
backup       &
gitmuch_save &
fetch_mail   &
#jobs
wait
jobs
echo

update_db
gitmuch_load
autotag

# Update the display in awesome
n=$(notmuch count tag:unread)
echo "tb_mails_updating(nil)\ntb_mails_set_count($n)" | awesome-client
