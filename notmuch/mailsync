#!/bin/zsh

MAILDIR=$HOME/mail
BACKUPDIR=$HOME/.config/notmuch

NMSYNC=$HOME/dev/notmuch/notmuchsync/notmuchsync
MDSYNC="python -W ignore $(which offlineimap)"
TAGSCRIPT=$BACKUPDIR/autotag

# Already running?
zmodload zsh/net/socket
SOCKET=/tmp/mailsync.$UID
zsocket -l "$SOCKET" 2>/dev/null
if [ $? != 0 ]; then
    echo "Process already running."
    exit 1
fi

TRAPEXIT() {
    rm "$SOCKET"
}
TRAPINT() {
    return $(( 128 + $1 ))
}

# Quiet mode?
quiet=
if [ "$1" = "-q" ]; then quiet=1; fi

# Check the DISPLAY environment variable (needed for D-Bus and awesome-client)
[[ -z "$DISPLAY" ]] && export DISPLAY=:0.0

# Preload notmuch database
nice -n 15 cat $MAILDIR/.notmuch/xapian/* >/dev/null &

# Is a recent backup available?
BACKUPFILE=$BACKUPDIR/db-$(date +"%Y%m%d").xz

if [ ! -e "$BACKUPFILE" ]; then
    if [ ! $quiet ]; then  echo "Saving the notmuch database..."; fi
    notmuch dump | xz > "$BACKUPFILE" || exit 1

    # Remove backups older than a month
    find "$BACKUPDIR" -name 'db-*.xz' -and -mtime +30 -exec rm \{\} +
fi

# Is a network connection available?
if ! fping -q 8.8.8.8; then
    if [ ! $quiet ]; then echo "No network connection"; fi
    exit 11
fi

# Tell awesome the update begins
n=$(/usr/bin/notmuch count tag:unread)
echo "tb_mails_updating(true)\ntb_mails_set_count($n)" | awesome-client

# Run notmuchsync, then offlineimap, then the autotag script
if [ $quiet ]; then
    eval $NMSYNC -q -s && eval $MDSYNC -u Noninteractive.Quiet && eval $TAGSCRIPT >/dev/null
else
    eval $NMSYNC -s && eval $MDSYNC && eval $TAGSCRIPT
fi

# Update the display in awesome
n=$(/usr/bin/notmuch count tag:unread)
echo "tb_mails_updating(nil)\ntb_mails_set_count($n)" | awesome-client
