#!/bin/zsh

MAILDIR=$HOME/mail
BACKUPDIR=$HOME/.config/notmuch

# Use the SCHED_IDLEPRIO scheduling policy
schedtool -D $$

# Already running?
zmodload zsh/net/socket
SOCKET=/tmp/mailsync.$UID
zsocket -l "$SOCKET" 2>/dev/null
if [ $? != 0 ]; then
    echo "Process already running."
    exit 1
fi

zshexit() {
    rm "$SOCKET"
}
TRAPINT() {
    return $(( 128 + $1 ))
}

# Parse command line options
quiet=
while getopts fhqs OPT; do
    case $OPT in
        f) MDSYNC="$MDSYNC -q" ;;
        q) quiet=1 ;;
        s) MDSYNC="$MDSYNC -1" ;;
        *)
            echo "$0 [options]"
            echo ""
            echo "Available options:"
            echo "  -f  fast mode (don't sync IMAP flags)"
            echo "  -q  quiet mode (no output)"
            echo "  -s  single-threaded mode"
            exit 1 ;;
    esac
done

# Check the DISPLAY environment variable (needed for D-Bus and awesome-client)
[[ -z "$DISPLAY" ]] && export DISPLAY=:0.0

# Is a recent backup available?
BACKUPFILE=$BACKUPDIR/db-$(date +"%Y%m%d").xz

if [ ! -e "$BACKUPFILE" ]; then
    if [ ! $quiet ]; then  echo "Saving the notmuch database..."; fi
    notmuch dump | xz > "$BACKUPFILE" || exit 1

    # Remove backups older than a month
    find "$BACKUPDIR" -name 'db-*.xz' -and -mtime +30 -exec rm \{\} +
fi

# Is a network connection available?
if ! fping -q 8.8.8.8; then
    if [ ! $quiet ]; then echo "No network connection"; fi
    exit 11
fi

# Tell awesome the update begins
n=$(/usr/bin/notmuch count tag:unread)
echo "tb_mails_updating(true)\ntb_mails_set_count($n)" | awesome-client

# Set the Maildir T flag on message with the notmuch "deleted" tag
(( $(notmuch count tag:deleted) > 0 )) && notmuch search --output=files tag:deleted | xargs -d'\n' rm

# Run notmuchsync, then offlineimap, then the autotag script
if [ $quiet ]; then
    offlineimap -o -u Noninteractive.Quiet && nosync $BACKUPDIR/autotag >/dev/null
else
    offlineimap -o && nosync $BACKUPDIR/autotag
fi

# Update the display in awesome
n=$(/usr/bin/notmuch count tag:unread)
echo "tb_mails_updating(nil)\ntb_mails_set_count($n)" | awesome-client
